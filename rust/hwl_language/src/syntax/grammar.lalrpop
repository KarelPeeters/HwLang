use crate::syntax::pos::{Span, Spanned};
use crate::syntax::ast::*;
use crate::syntax::ParseContext;
use crate::syntax::token::{TokenType, TokenError};
use crate::util::arena::Arena;

grammar(ctx: &mut ParseContext, arena_expr: &mut Arena<ExpressionKindIndex, ExpressionKind>);

extern {
    type Location = usize;
    type Error = TokenError;

    enum TokenType {
        TokenBlockComment => TokenType::BlockComment,
        TokenLineComment => TokenType::LineComment,
        TokenIdentifier => TokenType::Identifier,
        TokenIntLiteralBinary => TokenType::IntLiteralBinary,
        TokenIntLiteralDecimal => TokenType::IntLiteralDecimal,
        TokenIntLiteralHexadecimal => TokenType::IntLiteralHexadecimal,
        TokenStringStart => TokenType::StringStart,
        TokenStringEnd => TokenType::StringEnd,
        TokenStringSubStart => TokenType::StringSubStart,
        TokenStringSubEnd => TokenType::StringSubEnd,
        TokenStringMiddle => TokenType::StringMiddle,
        "import" => TokenType::Import,
        "type" => TokenType::Type,
        "struct" => TokenType::Struct,
        "enum" => TokenType::Enum,
        "ports" => TokenType::Ports,
        "module" => TokenType::Module,
        "interface" => TokenType::Interface,
        "instance" => TokenType::Instance,
        "fn" => TokenType::Fn,
        "comb" => TokenType::Comb,
        "clock" => TokenType::Clock,
        "clocked" => TokenType::Clocked,
        "const" => TokenType::Const,
        "val" => TokenType::Val,
        "var" => TokenType::Var,
        "wire" => TokenType::Wire,
        "reg" => TokenType::Reg,
        "in" => TokenType::In,
        "out" => TokenType::Out,
        "async" => TokenType::Async,
        "sync" => TokenType::Sync,
        "return" => TokenType::Return,
        "break" => TokenType::Break,
        "continue" => TokenType::Continue,
        "true" => TokenType::True,
        "false" => TokenType::False,
        "undef" => TokenType::Undef,
        "if" => TokenType::If,
        "else" => TokenType::Else,
        "loop" => TokenType::Loop,
        "match" => TokenType::Match,
        "for" => TokenType::For,
        "while" => TokenType::While,
        "pub" => TokenType::Pub,
        "as" => TokenType::As,
        "external" => TokenType::External,
        "__builtin" => TokenType::Builtin,
        "unsafe_value_with_domain" => TokenType::UnsafeValueWithDomain,
        "id_from_str" => TokenType::IdFromStr,
        ";" => TokenType::Semi,
        ":" => TokenType::Colon,
        "," => TokenType::Comma,
        "->" => TokenType::Arrow,
        "=>" => TokenType::DoubleArrow,
        "_" => TokenType::Underscore,
        "::" => TokenType::ColonColon,
        "{" => TokenType::OpenC,
        "}" => TokenType::CloseC,
        "(" => TokenType::OpenR,
        ")" => TokenType::CloseR,
        "[" => TokenType::OpenS,
        "]" => TokenType::CloseS,
        "." => TokenType::Dot,
        ".." => TokenType::DotDot,
        "..=" => TokenType::DotDotEq,
        "+.." => TokenType::PlusDotDot,
        "&&" => TokenType::AmperAmper,
        "||" => TokenType::PipePipe,
        "^^" => TokenType::CaretCaret,
        "==" => TokenType::EqEq,
        "!=" => TokenType::Neq,
        ">=" => TokenType::Gte,
        ">" => TokenType::Gt,
        "<=" => TokenType::Lte,
        "<" => TokenType::Lt,
        "&" => TokenType::Amper,
        "|" => TokenType::Pipe,
        "^" => TokenType::Caret,
        "<<" => TokenType::LtLt,
        ">>" => TokenType::GtGt,
        "+" => TokenType::Plus,
        "-" => TokenType::Minus,
        "*" => TokenType::Star,
        "/" => TokenType::Slash,
        "%" => TokenType::Percent,
        "!" => TokenType::Bang,
        "**" => TokenType::StarStar,
        "=" => TokenType::Eq,
        "+=" => TokenType::PlusEq,
        "-=" => TokenType::MinusEq,
        "*=" => TokenType::StarEq,
        "/=" => TokenType::SlashEq,
        "%=" => TokenType::PercentEq,
        "&=" => TokenType::AmperEq,
        "|=" => TokenType::PipeEq,
        "^=" => TokenType::CaretEq,
    }
}

pub FileItems: Vec<Item> = {
    Item*,
}

// items
Item: Item = {
    <start:@L> "import" <parents:Spanned<(<Id> ".")+>> <entry:Spanned<ImportFinalKind>> ";" <end:@R>
        => Item::Import(ItemImport { parents, entry, span: ctx.span(start, end) }),

    <decl:Spanned<CommonDeclaration<Vis>>>
        => Item::CommonDeclaration(decl),

    <start:@L>
        // TODO make ports optional
        <vis:Vis> "module" <id:MaybeId>
        <params:(<Parameters>)?>
        <ports:ModulePorts>
        <body:Block<ModuleStatement>>
    <end:@R> => {
        Item::ModuleInternal(ItemDefModuleInternal { vis, id, params, ports, body, span: ctx.span(start, end) })
    },
    <start:@L>
        <vis:Vis> <span_ext:Span<"external">> "module" <id:Id>
        <params:(<Parameters>)?>
        <ports:ModulePorts>
    <end:@R> => {
        Item::ModuleExternal(ItemDefModuleExternal { vis, span_ext, id, params, ports, span: ctx.span(start, end) })
    },

    <start:@L>
        <vis:Vis> "interface" <id:MaybeId> <params:Parameters?> <start_body:@L>
        "{" <body:ExtraListWithBlockWithDecl<InterfacePortType, InterfaceView>> "}"
    <end:@R> => {
        let inter = ItemDefInterface {
            vis, id, params, body,
            span_body: ctx.span(start_body, end), span: ctx.span(start, end)
        };
        Item::Interface(inter)
    }
}

InterfacePortType: InterfaceListItem = {
    <port_id:Id> ":" <port_ty:Expr> => InterfaceListItem::PortType { port_id, port_ty },
}

InterfaceView: InterfaceListItem = {
    <start:@L> "interface" <id:MaybeId> "{" <port_dirs:ExtraListWithDecl<InterfacePortDir>> "}" <end:@R> => {
        InterfaceListItem::View(InterfaceView { id, port_dirs, span: ctx.span(start, end) })
    }
}

InterfacePortDir: (Identifier, Spanned<PortDirection>) = {
    <id:Id> ":" <dir:Spanned<PortDirection>> => (id, dir),
}

ImportFinalKind: ImportFinalKind = {
    ImportEntry => ImportFinalKind::Single(<>),
    "[" <Separated<ImportEntry,",">> "]" => ImportFinalKind::Multi(<>),
}

ImportEntry: ImportEntry = {
    <start:@L> <id:Id> <as_:("as" <MaybeId>)?> <end:@R>
        => ImportEntry { id, as_, span: ctx.span(start, end) },
}

EnumVariant: EnumVariant = {
    <start:@L> <id:Id> <content:("(" <Expr> ")")?> <end:@R>
        => EnumVariant { id, content, span: ctx.span(start, end) },
}

StructField: StructField = {
    <start:@L> <id:Id> ":" <ty:Expr> <end:@R>
        => StructField { id, ty, span: ctx.span(start, end) },
}

Block<S>: Block<S> = {
    <start:@L> "{" <statements:S*> "}" <end:@R> => {
        Block { statements, span: ctx.span(start, end) }
    },
}

BlockSeparated<T, S>: Block<T> = {
    <start:@L> "{" <statements:Separated<T, S>> "}" <end:@R> => {
        Block { statements, span: ctx.span(start, end) }
    },
}

ModuleStatement: ModuleStatement = Spanned<ModuleStatementKind>;
BlockStatement: BlockStatement = Spanned<BlockStatementKind>;

ModuleStatementKind: ModuleStatementKind = {
    // error recovery
//    ! => {
//        ctx.push_error(<>);
//        ModuleStatementKind::ParseError
//    },

    // control flow
    Block<ModuleStatement> => ModuleStatementKind::Block(<>),
    IfStatement<Block<ModuleStatement>> => ModuleStatementKind::If(<>),
    ForStatement<ModuleStatement> => ModuleStatementKind::For(<>),
    // declaration
    <CommonDeclaration<()>> => ModuleStatementKind::CommonDeclaration(<>),
    <vis:Vis> <span_keyword:Span<"reg">> <id:MaybeGeneralId> ":" <sync:(<Spanned<SyncDomain>>)?> <ty:Expr> "=" <init:Expr> ";" => {
        let decl = RegDeclaration { vis, span_keyword, id, sync, ty, init };
        ModuleStatementKind::RegDeclaration(decl)
    },
    <vis:Vis> <span_keyword:Span<"wire">> <id:MaybeGeneralId> <kind:WireDeclarationKind> ";" => {
        let decl = WireDeclaration { vis, span_keyword, id, kind };
        ModuleStatementKind::WireDeclaration(decl)
    },
    // marker
    "reg" "out" <id:Id> "=" <init:Expr> ";" => {
        let decl = RegOutPortMarker { id, init };
        ModuleStatementKind::RegOutPortMarker(decl)
    },
    // combinatorial block
    <span_keyword:Span<"comb">> <block:Block<BlockStatement>> => {
        let inner = CombinatorialBlock {
            block,
            span_keyword,
        };
        ModuleStatementKind::CombinatorialBlock(inner)
    },
    // clocked block
    // TODO why is reset still part of the syntax here? only the registers themselves care, not the block?
    //   registers declared inside of the block do still inherit their clock/reset from the block though
    <span_keyword:Span<"clocked">> <start_domain:@L> "("
        <clock:Expr> <reset:("," <Spanned<ClockedBlockReset>>)?> ","?
    ")" <end_domain:@R> <block:Block<BlockStatement>>
    => {
        let inner = ClockedBlock {
            clock,
            reset,
            block,
            span_keyword,
            span_domain: ctx.span(start_domain, end_domain),
        };
        ModuleStatementKind::ClockedBlock(inner)
    },
    // module instance
    // TODO allow strings as instance names? necessary for modules instantiated in loops
    // TODO allow "if" here?
    <start:@L>
        "instance" <instance_end:@R> <name:(<Id> "=")?> <module:Expr>
        "ports" <port_connections:Spanned<InstancePortConnections>>
        ";"
    <end:@R> => {
        let instance = ModuleInstance {
            span_keyword: ctx.span(start, instance_end),
            name,
            module,
            port_connections,
        };
        ModuleStatementKind::Instance(instance)
    },
}

// TODO make this an extra list
InstancePortConnections: Vec<Spanned<PortConnection>> = {
    "(" <Separated<Spanned<PortConnection>,",">> ")"
}

// TODO find a way to avoid having to create a fake expression for the value here
PortConnection: PortConnection = {
    <id:Id> <expr:("=" <Expr>)?> => {
        let expr = match expr {
            None => {
                let kind = ExpressionKind::Id(GeneralIdentifier::Simple(id.clone()));
                let expr = Spanned::new(id.span, arena_expr.push(kind));
                PortConnectionExpression::FakeId(expr)
            },
            Some(expr) => PortConnectionExpression::Real(expr),
        };
        PortConnection { id, expr }
    }
}

ClockedBlockReset: ClockedBlockReset<Expression> = {
    <kind:Spanned<ResetKind>> <signal:Expr> => ClockedBlockReset { kind, signal },
}

ResetKind: ResetKind = {
    "sync" => ResetKind::Sync,
    "async" => ResetKind::Async,
}

BlockStatementKind: BlockStatementKind = {
    // declaration
    <CommonDeclaration<()>> => BlockStatementKind::CommonDeclaration(<>),
    <VariableDeclaration> => BlockStatementKind::VariableDeclaration(<>),
    // assignment
    <start:@L> <target:Expr> <op:AssignOp> <value:Expr> ";" <end:@R> => {
        let assignment = Assignment { op, target, value, span: ctx.span(start, end) };
        BlockStatementKind::Assignment(assignment)
    },
    // expression
    // TODO semicolon is optional for control flow statements
    <start:@L> <inner:Expr> ";" <end:@R> => {
        BlockStatementKind::Expression(inner)
    },
    // block
    <Block<BlockStatement>> => BlockStatementKind::Block(<>),
    // control flow
    IfStatement<Block<BlockStatement>>
        => BlockStatementKind::If(<>),
    MatchStatement<Block<BlockStatement>>
        => BlockStatementKind::Match(<>),
    ForStatement<BlockStatement>
        => BlockStatementKind::For(<>),
    // TODO don't require parentheses?
    <span_keyword:Span<"while">> "(" <cond:Expr> ")" <body:Block<BlockStatement>>
        => BlockStatementKind::While(WhileStatement { span_keyword, cond, body }),
    // control flow terminators
    <span_return:Span<"return">> <value:Expr?> ";"
        => BlockStatementKind::Return(ReturnStatement { span_return, value }),
    <span:Span<"break">> ";" => BlockStatementKind::Break { span },
    <span:Span<"continue">> ";" => BlockStatementKind::Continue { span },
}

// TODO don't require parentheses?
IfStatement<B>: IfStatement<B> = {
    <start:@L> <span_if:Span<"if">> "(" <cond:Expr> ")" <then_block:B> <end_if:@R>
    <else_ifs:ElseIfPair<B>*>
    <final_else:("else" <B>)?> <end:@R> => {
        IfStatement {
            span: ctx.span(start, end),
            initial_if: IfCondBlockPair {
                span: ctx.span(start, end_if),
                span_if: span_if,
                cond: cond,
                block: then_block
            },
            else_ifs,
            final_else
        }
    },
}

// TODO don't require parentheses?
ElseIfPair<B>: IfCondBlockPair<B> = {
    <start:@L> "else" <span_if:Span<"if">> "(" <cond:Expr> ")" <block:B> <end:@R>
        => IfCondBlockPair { cond, block, span: ctx.span(start, end), span_if },
}

// TODO don't require parentheses?
// TODO make the branches an extra list?
MatchStatement<B>: MatchStatement<B> = {
    "match" "(" <target:Expr> ")" "{" <branches:ExtraListWithBlockNoDecl<Never<MatchBranch<B>>, MatchBranch<B>>> <pos_end:@L> "}"
        => MatchStatement { target, branches, pos_end: ctx.pos(pos_end) },
}

MatchBranch<B>: MatchBranch<B> = {
    <pattern:Spanned<MatchPattern>> "=>" <block:B> => MatchBranch { pattern, block },
}

MatchPattern: MatchPattern = {
    <value:Expr> => {
        match &arena_expr[value.inner] {
            ExpressionKind::Dummy => MatchPattern::Wildcard,
            _ => MatchPattern::EqualTo(value),
        }
    },
    "val" <id:MaybeId> => MatchPattern::WildcardVal(id),
    <span_in:Span<"in">> <range:Expr> => MatchPattern::InRange { span_in, range },
    "." <variant:Id> <payload_id:("(" <MaybeValId> ")")?> => MatchPattern::IsEnumVariant { variant, payload_id },
}

MaybeValId: MaybeIdentifier = {
    // extra val token to distinguish from ids used in expressions
    "val" <Id> => MaybeIdentifier::Identifier(<>),
    Span<"_"> => MaybeIdentifier::Dummy { span: <> },
}

ForStatement<S>: ForStatement<S> = {
    // TODO don't require parentheses?
    <span_keyword:Span<"for">> "(" <index:MaybeId> <index_ty: (":" <ExprNoIn>)?> "in" <iter:ExprNoIn> ")" <body:Block<S>>
        => ForStatement { span_keyword, index, index_ty, iter, body },
}

CommonDeclaration<V>: CommonDeclaration<V> = {
    <vis:V> <kind:CommonDeclarationNamedKind>
        => CommonDeclaration::Named(CommonDeclarationNamed { vis, kind }),
    <span_keyword:Span<"const">> <block:Block<BlockStatement>>
        => CommonDeclaration::ConstBlock(ConstBlock { span_keyword, block }),
}

CommonDeclarationNamedKind: CommonDeclarationNamedKind = {
    <start:@L> "type" <id:MaybeId> <params:Parameters?> "=" <body:Expr> ";" <end:@R>
        => CommonDeclarationNamedKind::Type(TypeDeclaration { id, params, body, span: ctx.span(start, end) }),

    <start:@L> "const" <id:MaybeId> <ty:(":" <Expr>)?> "=" <value:Expr> ";" <end:@R>
        => CommonDeclarationNamedKind::Const(ConstDeclaration { id, ty, value, span: ctx.span(start, end) }),

    <start:@L>
        "struct" <id:MaybeId> <params:Parameters?>
        <start_body:@L> "{" <fields: ExtraListWithDecl<StructField>> "}"
    <end:@R>
        => CommonDeclarationNamedKind::Struct(StructDeclaration { id, params, fields, span: ctx.span(start, end), span_body: ctx.span(start_body, end) }),

    <start:@L>
        "enum" <id:MaybeId> <params:Parameters?>
        "{" <variants: ExtraListWithDecl<EnumVariant>> "}"
    <end:@R>
        => CommonDeclarationNamedKind::Enum(EnumDeclaration { id, params, variants, span: ctx.span(start, end) }),

    // We only allow tightly-bounding expressions in the return type to avoid (formal) ambiguity.
    // TODO allow specifying "const" before function params to make error messages nicer
    <start:@L>
        "fn" <id:MaybeId> <params:Parameters>
        <ret_ty:("->" <ExprNoBlock>)?>
        <body:Block<BlockStatement>>
    <end:@R>
        => CommonDeclarationNamedKind::Function(FunctionDeclaration { id, params, ret_ty, body, span: ctx.span(start, end) }),
}

VariableDeclaration: VariableDeclaration = {
    <start:@L> <mutable:VariableMutable> <id:MaybeId> <ty: (":"<Expr>)?> <init: ("="<Expr>)?> ";" <end:@R> => {
        VariableDeclaration { mutable, id, ty, init, span: ctx.span(start, end) }
    },
}

VariableMutable: bool = {
    "val" => false,
    "var" => true,
}

// parameters
Parameters: Parameters = {
    <start:@L> "(" <items:ExtraListWithDecl<Parameter>> ")" <end:@R>
        => Parameters { items, span: ctx.span(start, end) },
}

Parameter: Parameter = {
    <start:@L> <id:Id> ":" <ty:Expr> <default:("=" <Expr>)?> <end:@R>
        => Parameter { id, ty, default, span: ctx.span(start, end) },
}

ModulePorts: Spanned<ExtraList<ModulePortItem>> = {
    <start_ports:@L> "ports" "(" <ports:ExtraListWithBlockWithDecl<ModulePortItemPort, ModulePortItemBlock>> ")" <end_ports:@R>
        => Spanned { inner: ports, span: ctx.span(start_ports, end_ports) },
}

ModulePortItemPort: ModulePortItem = {
    <start:@L> <id:Id> ":" <kind:ModulePortSingleKind> <end:@R>
        => ModulePortItem::Single(ModulePortSingle { id, kind, span: ctx.span(start, end) }),
}

ModulePortItemBlock: ModulePortItem = {
    // TODO allow const domain here again, it conflicts with const blocks
    <start:@L> <domain:Spanned<AsyncOrSyncDomain>> <ports:ExtraBlockWithDecl<ModulePortInBlock, Never<ModulePortInBlock>>> <end:@R>
        => ModulePortItem::Block(ModulePortBlock { domain, ports, span: ctx.span(start, end) }),
}

ModulePortInBlock: ModulePortInBlock = {
    <start:@L> <id:Id> ":" <kind:ModulePortInBlockKind> <end:@R>
        => ModulePortInBlock { id, kind, span: ctx.span(start, end) },
}

ModulePortSingleKind: ModulePortSingleKind = {
    <direction:Spanned<PortDirection>> <kind:PortSingleKindInner>
        => ModulePortSingleKind::Port { direction, kind },
    <span_keyword:Span<"interface">> <domain:Spanned<DomainKind>> <interface:Expr>
        => ModulePortSingleKind::Interface { span_keyword, domain, interface },
}

PortSingleKindInner: PortSingleKindInner = {
    <span_clock:Span<"clock">> => PortSingleKindInner::Clock { span_clock },
    <domain:Spanned<DomainKind>> <ty:Expr> => PortSingleKindInner::Normal { domain, ty },
}

ModulePortInBlockKind: ModulePortInBlockKind = {
    <direction:Spanned<PortDirection>> <ty:Expr>
        => ModulePortInBlockKind::Port { direction, ty },
    <span_keyword:Span<"interface">> <interface:Expr>
        => ModulePortInBlockKind::Interface { span_keyword, interface },
}

ExtraListNoDecl<I> = ExtraListImpl<I, Never<I>, Never<CommonDeclaration<()>>>;
ExtraListWithDecl<I> = ExtraListImpl<I, Never<I>, CommonDeclaration<()>>;
ExtraListWithBlockNoDecl<I, B> = ExtraListImpl<I, B, Never<CommonDeclaration<()>>>;
ExtraListWithBlockWithDecl<I, B> = ExtraListImpl<I, B, CommonDeclaration<()>>;

ExtraBlockWithDecl<I, B> = ExtraBlockImpl<I, B, CommonDeclaration<()>>;

ExtraListImpl<I, B, D>: ExtraList<I> = {
    <items:Spanned<ExtraListItems<I, B, D>>> => {
        ExtraList { items: items.inner, span: items.span }
    }
}

ExtraBlockImpl<I, B, D>: ExtraList<I> = {
    "{" <items:ExtraListImpl<I, B, D>> "}" => <>,
}

ExtraListItems<I, B, D>: Vec<ExtraItem<I>> = {
    <items:ExtraItem<I, B, D>*> <last:I?> => {
        let mut items = items;
        if let Some(last) = last {
            items.push(ExtraItem::Inner(last));
        }
        items
    }
}

ExtraItem<I, B, D>: ExtraItem<I> = {
    // leaf inner item with trailing comma (the potential final item without a trailing comma is handled in ExtraListItems)
    <I> "," => ExtraItem::Inner(<>),
    // leaf inner block, there should include a trailing separator if necessary,
    //   typically not because they already end in "}"
    <B> => ExtraItem::Inner(<>),

    // leaf declaration, these should include a trailing separator if necessary, typically ";"
    <D> => ExtraItem::Declaration(<>),
    
    // control flow
    IfStatement<ExtraBlockImpl<I, B, D>> => ExtraItem::If(<>),
    // TODO add match, for, maybe even while?
}

WireDeclarationKind: WireDeclarationKind = {
    <domain_ty:WireDeclarationDomainTyKind> <assign_span_and_value:(Span<"="> Expr)?>
        => WireDeclarationKind::Normal { domain_ty, assign_span_and_value },
    ":" <domain:Spanned<DomainKind>?> <span_keyword:Span<"interface">> <interface:Expr>
        => WireDeclarationKind::Interface { domain, span_keyword, interface },
}

WireDeclarationDomainTyKind: WireDeclarationDomainTyKind = {
    ":" <span_clock:Span<"clock">>
        => WireDeclarationDomainTyKind::Clock { span_clock },
    
    // manually expanded because ":" should be present iff at least one of domain or ty is present
    ":" <domain:Spanned<DomainKind>> <ty:Expr>
        => WireDeclarationDomainTyKind::Normal { domain: Some(domain), ty: Some(ty) },
    ":" <domain:Spanned<DomainKind>>
        => WireDeclarationDomainTyKind::Normal { domain: Some(domain), ty: None },
    ":" <ty:Expr>
        => WireDeclarationDomainTyKind::Normal { domain: None, ty: Some(ty) },
    ()
        => WireDeclarationDomainTyKind::Normal { domain: None, ty: None },
}

PortDirection: PortDirection = {
    "in" => PortDirection::Input,
    "out" => PortDirection::Output,
}

DomainKind: DomainKind<Expression> = {
    "const" => DomainKind::Const,
    AsyncOrSyncDomain,
}

AsyncOrSyncDomain: DomainKind<Expression> = {
   "async" => DomainKind::Async,
   <SyncDomain> => DomainKind::Sync(<>),
}

// TODO make this more consistent with processes, "sync(clk, async rst)"
SyncDomain: SyncDomain<Expression> = {
    "sync" "(" <clock:Expr> <reset:("," "async" <Expr>)?> ","? ")" => SyncDomain { clock, reset },
}

// expressions
Expr: Expression = SpannedPushArena<ExprLevelIn>;
ExprNoIn: Expression = SpannedPushArena<ExprLevelRange>;
// TODO why this random intermediate level?
ExprNoBlock: Expression = SpannedPushArena<ExprLevelArrayImpl<"false", "true", "true">>;

PushArena<T>: ExpressionKindIndex = {
    T => arena_expr.push(<>)
}
SpannedPushArena<T>: Expression = Spanned<PushArena<T>>;

ExprLevelIn: ExpressionKind = {
    <value:Spanned<ExprLevelRange>> <op:Span<"in">> <range:Spanned<ExprLevelRange>>
        => build_binary_op(arena_expr, op, BinaryOp::In, value, range),
    ExprLevelRange,
}

ExprLevelRange: ExpressionKind = {
    // assoc none
    <start:SpannedPushArena<ExprLevelLogical>?> <op_span:Span<"..">> <end:SpannedPushArena<ExprLevelLogical>?>
        => ExpressionKind::RangeLiteral(RangeLiteral::ExclusiveEnd { op_span, start, end }),
    <start:SpannedPushArena<ExprLevelLogical>?> <op_span:Span<"..=">> <end_inc:SpannedPushArena<ExprLevelLogical>>
        => ExpressionKind::RangeLiteral(RangeLiteral::InclusiveEnd { op_span, start, end_inc }),
    <start:SpannedPushArena<ExprLevelLogical>> <op_span:Span<"+..">> <length:SpannedPushArena<ExprLevelLogical>>
        => ExpressionKind::RangeLiteral(RangeLiteral::Length { op_span, start, length }),
    ExprLevelLogical,
}

ExprLevelLogical: ExpressionKind = {
    // assoc left, no mixing
    <left:Spanned<ExprLevelLogicalAnd>> <op:Span<"&&">> <right:Spanned<ExprLevelEqual>> =>
        build_binary_op(arena_expr, op, BinaryOp::BoolAnd, left, right),
    <left:Spanned<ExprLevelLogicalOr>> <op:Span<"||">> <right:Spanned<ExprLevelEqual>> =>
        build_binary_op(arena_expr, op, BinaryOp::BoolOr, left, right),
    <left:Spanned<ExprLevelLogicalXor>> <op:Span<"^^">> <right:Spanned<ExprLevelEqual>> =>
        build_binary_op(arena_expr, op, BinaryOp::BoolXor, left, right),
    ExprLevelEqual,
}
ExprLevelLogicalAnd: ExpressionKind = {
    <left:Spanned<ExprLevelLogicalAnd>> <op:Span<"&&">> <right:Spanned<ExprLevelEqual>> =>
        build_binary_op(arena_expr, op, BinaryOp::BoolAnd, left, right),
    ExprLevelEqual,
}
ExprLevelLogicalOr: ExpressionKind = {
    <left:Spanned<ExprLevelLogicalOr>> <op:Span<"||">> <right:Spanned<ExprLevelEqual>> =>
        build_binary_op(arena_expr, op, BinaryOp::BoolOr, left, right),
    ExprLevelEqual,
}
ExprLevelLogicalXor: ExpressionKind = {
    <left:Spanned<ExprLevelLogicalXor>> <op:Span<"^^">> <right:Spanned<ExprLevelEqual>> =>
        build_binary_op(arena_expr, op, BinaryOp::BoolXor, left, right),
    ExprLevelEqual,
}

ExprLevelEqual: ExpressionKind = {
    // assoc none
    <left:Spanned<ExprLevelBitWise>> <op:Span<"==">> <right:Spanned<ExprLevelBitWise>> =>
        build_binary_op(arena_expr, op, BinaryOp::CmpEq, left, right),
    <left:Spanned<ExprLevelBitWise>> <op:Span<"!=">> <right:Spanned<ExprLevelBitWise>> =>
        build_binary_op(arena_expr, op, BinaryOp::CmpNeq, left, right),
    <left:Spanned<ExprLevelBitWise>> <op:Span<">=">> <right:Spanned<ExprLevelBitWise>> =>
        build_binary_op(arena_expr, op, BinaryOp::CmpGte, left, right),
    <left:Spanned<ExprLevelBitWise>> <op:Span<">">> <right:Spanned<ExprLevelBitWise>> =>
        build_binary_op(arena_expr, op, BinaryOp::CmpGt, left, right),
    <left:Spanned<ExprLevelBitWise>> <op:Span<"<=">> <right:Spanned<ExprLevelBitWise>> =>
        build_binary_op(arena_expr, op, BinaryOp::CmpLte, left, right),
    <left:Spanned<ExprLevelBitWise>> <op:Span<"<">> <right:Spanned<ExprLevelBitWise>> =>
        build_binary_op(arena_expr, op, BinaryOp::CmpLt, left, right),
    ExprLevelBitWise,
}

ExprLevelBitWise: ExpressionKind = {
    // assoc left, no mixing
    <left:Spanned<ExprLevelBitAnd>> <op:Span<"&">> <right:Spanned<ExprLevelShift>> =>
        build_binary_op(arena_expr, op, BinaryOp::BitAnd, left, right),
    <left:Spanned<ExprLevelBitOr>> <op:Span<"|">> <right:Spanned<ExprLevelShift>> =>
        build_binary_op(arena_expr, op, BinaryOp::BitOr, left, right),
    <left:Spanned<ExprLevelBitXor>> <op:Span<"^">> <right:Spanned<ExprLevelShift>> =>
        build_binary_op(arena_expr, op, BinaryOp::BitXor, left, right),
    ExprLevelShift,
}
ExprLevelBitAnd: ExpressionKind = {
    <left:Spanned<ExprLevelBitAnd>> <op:Span<"&">> <right:Spanned<ExprLevelShift>> =>
        build_binary_op(arena_expr, op, BinaryOp::BitAnd, left, right),
    ExprLevelShift,
}
ExprLevelBitOr: ExpressionKind = {
    <left:Spanned<ExprLevelBitOr>> <op:Span<"|">> <right:Spanned<ExprLevelShift>> =>
        build_binary_op(arena_expr, op, BinaryOp::BitOr, left, right),
    ExprLevelShift,
}
ExprLevelBitXor: ExpressionKind = {
    <left:Spanned<ExprLevelBitXor>> <op:Span<"^">> <right:Spanned<ExprLevelShift>> =>
        build_binary_op(arena_expr, op, BinaryOp::BitXor, left, right),
    ExprLevelShift,
}

ExprLevelShift: ExpressionKind = {
    // assoc none
    <left:Spanned<ExprLevelAdd>> <op:Span<"<<">> <right:Spanned<ExprLevelAdd>> =>
        build_binary_op(arena_expr, op, BinaryOp::Shl, left, right),
    <left:Spanned<ExprLevelAdd>> <op:Span<">>">> <right:Spanned<ExprLevelAdd>> =>
        build_binary_op(arena_expr, op, BinaryOp::Shr, left, right),
    ExprLevelAdd,
}

ExprLevelAdd: ExpressionKind = {
    // assoc left
    <left:Spanned<ExprLevelAdd>> <op:Span<"+">> <right:Spanned<ExprLevelMul>> =>
        build_binary_op(arena_expr, op, BinaryOp::Add, left, right),
    <left:Spanned<ExprLevelAdd>> <op:Span<"-">> <right:Spanned<ExprLevelMul>> =>
        build_binary_op(arena_expr, op, BinaryOp::Sub, left, right),
    ExprLevelMul,
}

ExprLevelMul: ExpressionKind = {
    // assoc left
    <left:Spanned<ExprLevelMul>> <op:Span<"*">> <right:Spanned<ExprLevelUnary>> =>
        build_binary_op(arena_expr, op, BinaryOp::Mul, left, right),
    <left:Spanned<ExprLevelMul>> <op:Span<"/">> <right:Spanned<ExprLevelUnary>> =>
        build_binary_op(arena_expr, op, BinaryOp::Div, left, right),
    // TODO disallow mixing this with mul and/or div?
    <left:Spanned<ExprLevelMul>> <op:Span<"%">> <right:Spanned<ExprLevelUnary>> =>
        build_binary_op(arena_expr, op, BinaryOp::Mod, left, right),
    ExprLevelUnary,
}

ExprLevelUnary: ExpressionKind = {
    <op:Span<"+">> <inner:SpannedPushArena<ExprLevelPower>> =>
        ExpressionKind::UnaryOp(Spanned { span: op, inner: UnaryOp::Plus }, inner),
    <op:Span<"-">> <inner:SpannedPushArena<ExprLevelPower>> =>
        ExpressionKind::UnaryOp(Spanned { span: op, inner: UnaryOp::Neg }, inner),
    <op:Span<"!">> <inner:SpannedPushArena<ExprLevelPower>> =>
        ExpressionKind::UnaryOp(Spanned { span: op, inner: UnaryOp::Not }, inner),
    ExprLevelPower,
}

ExprLevelPower: ExpressionKind = {
    // assoc none
    <left:Spanned<ExprLevelArray>> <op:Span<"**">> <right:Spanned<ExprLevelArray>> =>
        build_binary_op(arena_expr, op, BinaryOp::Pow, left, right),
    ExprLevelArray,
}

ExprLevelArray = ExprLevelArrayImpl<"true", "true", "true">;

ExprLevelArrayImpl<allow_block, allow_array_type, allow_array_literal>: ExpressionKind = {
    // TODO we need to use ArrayLiteralElement here to avoid parsing ambiguities, which is a bit strange
    //   eventually we want to support those anyway, so it's not that bad
    // array type
    <start_brackets:@L> "[" <lengths:Separated<ArrayLiteralElement,",">> "]" <end_brackets:@R>
    <inner_ty:SpannedPushArena<ExprLevelArrayImpl<allow_block, allow_array_type, "false">>>
        if allow_array_type == "true"
        => ExpressionKind::ArrayType { span_brackets: ctx.span(start_brackets, end_brackets), lengths, inner_ty },
    // array index
    <base:SpannedPushArena<ExprLevelArrayImpl<allow_block, "false", "false">>>
    <start_brackets:@L> "[" <indices:Separated<ArrayLiteralElement,",">> "]" <end_brackets:@R>
        => ExpressionKind::ArrayIndex { span_brackets: ctx.span(start_brackets, end_brackets), base, indices },

    // array literal
    "[" <Separated<ArrayLiteralElement,",">> "]"
        if allow_array_literal == "true"
        => ExpressionKind::ArrayLiteral(<>),
    // array comprehension
    "[" <body:ArrayLiteralElement> "for" <index:MaybeId> <span_keyword:Span<"in">> <iter:ExprNoIn> "]"
        if allow_array_literal == "true"
        => ExpressionKind::ArrayComprehension(ArrayComprehension { body, index, span_keyword, iter }),

    // tuple literal
    "(" <SeparatedAtLeastOneSep<Expr,",">> ")"
        => ExpressionKind::TupleLiteral(<>),
    "(" ")" => ExpressionKind::TupleLiteral(vec![]),

    ExprLevelCall<allow_block>,
}

ExprLevelCall<allow_block>: ExpressionKind = {
    // normal call
    <target:SpannedPushArena<ExprLevelCall<allow_block>>> "(" <args:Args> ")"
        => ExpressionKind::Call(target, args),

    // builtins
    "unsafe_value_with_domain" "(" <value:Expr> "," <domain:Spanned<DomainKind>> ","? ")"
        => ExpressionKind::UnsafeValueWithDomain(value, domain),

    // reg expression
    <span_keyword:Span<"reg">> "(" <value:Expr> "," <init:Expr> ","? ")"
        => ExpressionKind::RegisterDelay(RegisterDelay { span_keyword, value, init }),

    ExprLevelDot<allow_block>,
}

// TODO issue when trying to chain this with array indices, eg. a[0].b fails to parse
ExprLevelDot<allow_block>: ExpressionKind = {
    <target:SpannedPushArena<ExprLevelCall<allow_block>>> "." <index:DotIndexKind>
        => ExpressionKind::DotIndex(target, index),
    ExprLevelAtomic<allow_block>,
}

DotIndexKind: DotIndexKind = {
    <index:Id> => DotIndexKind::Id(index),
    <index:Span<TokenIntLiteralDecimal>> => DotIndexKind::Int { span: index },
}

// TODO allow "if" here
Args: Args = {
    <start:@L> <inner:Separated<Arg,",">> <end:@L> =>
        Args { inner, span: ctx.span(start, end) },
}

Arg: Arg = {
    <start:@L> <name:(<Id> "=")?> <value:Expr> <end:@L> =>
        Arg { name, value, span: ctx.span(start, end) },
}

ArrayLiteralElement: ArrayLiteralElement<Expression> = {
    // TODO allow multiple stars to flatten multi-dimensional arrays?
    <spread:Span<"*">> <value:Expr> => ArrayLiteralElement::Spread(spread, value),
    <value:Expr> => ArrayLiteralElement::Single(value),
}

ExprLevelAtomic<allow_block>: ExpressionKind = {
    // error recovery
    ! => ExpressionKind::ParseError(ctx.push_error(<>)),

    // parentheses grouping
    "(" <expr:Expr> ")" => ExpressionKind::Wrapped(expr),
    // block expression
    "{" <statements:BlockStatement*> <expression:Expr> "}" if allow_block == "true"
        => ExpressionKind::Block(BlockExpression { statements, expression }),
    // misc
    <id:GeneralId> => ExpressionKind::Id(id),
    "type" => ExpressionKind::Type,
    "fn" => ExpressionKind::TypeFunction,
    // TODO this should maybe not be an expression kind after all, there's only a few specific places it's allowed in
    "_" => ExpressionKind::Dummy,
    // literals
    "true" => ExpressionKind::BoolLiteral(true),
    "false" => ExpressionKind::BoolLiteral(false),
    "undef" => ExpressionKind::Undefined,
    "__builtin" => ExpressionKind::Builtin,
    <span:Span<TokenIntLiteralBinary>>
        => ExpressionKind::IntLiteral(IntLiteral::Binary { span }),
    <span:Span<TokenIntLiteralDecimal>>
        => ExpressionKind::IntLiteral(IntLiteral::Decimal { span }),
    <span:Span<TokenIntLiteralHexadecimal>>
        => ExpressionKind::IntLiteral(IntLiteral::Hexadecimal { span }),
    TokenStringStart <StringPiece*> TokenStringEnd
        => ExpressionKind::StringLiteral(<>),
}

StringPiece: StringPiece<Span, Expression> = {
    Span<TokenStringMiddle> => StringPiece::Literal(<>),
    TokenStringSubStart <Expr> TokenStringSubEnd => StringPiece::Substitute(<>),
}

//Path: Path = {
//    <start:@L> <steps: Spanned<(<Id> ".")*>> <id: Id> <end:@R>
//        => Path { span: ctx.span(start, end), steps, id }
//}

// terminals
Id: Identifier = {
    <span:Span<TokenIdentifier>> => Identifier { span },
}

GeneralId: GeneralIdentifier = {
    Id => GeneralIdentifier::Simple(<>),
    Spanned<GeneralIdFromString> => GeneralIdentifier::FromString(<>.span, <>.inner),
}

// TODO find better syntax for this and use it in more places
GeneralIdFromString: Expression = {
    "id_from_str" "(" <Expr> ","? ")",
}

MaybeId = MaybeIdImpl<Id>;
MaybeGeneralId = MaybeIdImpl<GeneralId>;

MaybeIdImpl<I>: MaybeIdentifier<I> = {
    <span:Span<"_">> => MaybeIdentifier::Dummy { span },
    I => MaybeIdentifier::Identifier(<>),
}

#[inline]
Vis: Visibility = {
    <span:Span<"pub">> => Visibility::Public { span },
    => Visibility::Private,
}

AssignOp: Spanned<Option<AssignBinaryOp>> = Spanned<AssignOpInner>;

AssignOpInner: Option<AssignBinaryOp> = {
    "=" => None,
    "+=" => Some(AssignBinaryOp::Add),
    "-=" => Some(AssignBinaryOp::Sub),
    "*=" => Some(AssignBinaryOp::Mul),
    "/=" => Some(AssignBinaryOp::Div),
    "%=" => Some(AssignBinaryOp::Mod),
    "&=" => Some(AssignBinaryOp::BitAnd),
    "|=" => Some(AssignBinaryOp::BitOr),
    "^=" => Some(AssignBinaryOp::BitXor),
}

// utilities
Separated<T, S>: Vec<T> = {
    <v:(<T> S)*> <e:T?> => {
        let mut v = v;
        if let Some(e) = e {
            v.push(e);
        }
        v
    }
}

SeparatedAtLeastOneSep<T, S>: Vec<T> = {
    <mut v:(<T> S)+> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

// Matches nothing, useful to skip branches in generic macros.
#[inline]
Never<T>: T = {}

#[inline]
Box<T>: Box<T> = {
    T => Box::new(<>),
}

#[inline]
BoxSpanned<T>: Box<Spanned<T>> = Box<Spanned<T>>;

#[inline]
Spanned<T>: Spanned<T> = {
    <start:@L> <inner:T> <end:@R> => Spanned { inner, span: ctx.span(start, end) },
}

#[inline]
Span<T>: Span = {
    <start:@L> <inner:T> <end:@R> => ctx.span(start, end),
}
